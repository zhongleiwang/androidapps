apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.protobuf'

buildscript {
    repositories {
      jcenter()
      google()
      mavenCentral()
    }
    dependencies {
        // replace with the current version of the Android plugin
        classpath 'com.android.tools.build:gradle:3.0.1'
        // the latest version of the android-apt plugin
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        // protobuf plugin
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.2'
        // firebase
        classpath 'com.google.gms:google-services:3.1.1'
        // firebase performance monitoring
        classpath 'com.google.firebase:firebase-plugins:1.1.1'
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.cwave.calculation"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    sourceSets {
        main {
            proto {
                // In addition to the default 'src/main/proto'
                srcDir 'src/main/proto'
                // In addition to the default '**/*.proto' (use with caution).
                // Using an extension other than 'proto' is NOT recommended,
                // because when proto files are published along with class files, we can
                // only tell the type of a file from its extension.
                include '**/*.protodevel'
            }
        }
        test {
            proto {
                // In addition to the default 'src/test/proto'
                srcDir 'src/test/protocolbuffers'
            }
        }
    }
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.0.0'
  }
  plugins {
    javalite {
      artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      javalite { }
    }
  }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:support-annotations:26.1.0'

    // multidex support prior to Android 5.0
    compile 'com.android.support:multidex:1.0.1'

    // Dagger core dependencies
    compile 'com.google.dagger:dagger:2.11'
    compile 'com.google.dagger:dagger-android:2.11'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    // Annotation parcel extension, https://github.com/rharter/auto-value-parcel
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

    // Date
    compile 'net.danlew:android.joda:2.9.3.1'
    compile 'com.ocpsoft:ocpsoft-pretty-time:1.0.7'

    // otto
    compile 'com.squareup:otto:1.3.4'

    // Guava
    compile 'com.google.guava:guava:23.0-android'

    // https://github.com/google/dagger/issues/753
    annotationProcessor 'com.google.guava:guava:23.0'

    // auto value
    annotationProcessor 'com.google.auto.value:auto-value:1.5'

    // protobuf
    compile 'com.google.protobuf:protobuf-lite:3.0.0'

    // protobuf-java-format
    compile 'com.googlecode.protobuf-java-format:protobuf-java-format:1.4'

    // firebase
    compile 'com.google.firebase:firebase-core:11.4.2'
    compile 'com.google.firebase:firebase-config:11.4.2'
    compile 'com.google.firebase:firebase-crash:11.4.2'
    compile 'com.google.firebase:firebase-database:11.4.2'
    compile 'com.google.firebase:firebase-firestore:11.4.2'
    compile 'com.google.firebase:firebase-messaging:11.4.2'
    compile 'com.google.firebase:firebase-perf:11.4.2'
    compile 'com.google.firebase:firebase-storage:11.4.2'

    // firebase-jobdispatcher
    compile 'com.firebase:firebase-jobdispatcher:0.6.0'

    // FirebaseUI (for authentication)
    compile 'com.firebaseui:firebase-ui:3.1.0'

    // FirebaseUI (for authentication)
    compile 'com.firebaseui:firebase-ui-auth:3.1.0'

    // FirebaseUI for Firebase Realtime Database
    compile 'com.firebaseui:firebase-ui-database:2.1.0'

    // FirebaseUI for Cloud Firestore
    compile 'com.firebaseui:firebase-ui-firestore:3.1.0'

    // FirebaseUI for Cloud Storage
    compile 'com.firebaseui:firebase-ui-storage:3.1.0'

    // Required only if Facebook login support is required
    compile 'com.facebook.android:facebook-login:4.27.0'

    // Required only if Twitter login support is required
    compile('com.twitter.sdk.android:twitter-core:3.0.0@aar') { transitive = true }

    // proto
    compile project(':proto')

    // junit
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
